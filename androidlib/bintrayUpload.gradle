apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def siteUrl = 'https://github.com/as11051105/MyAndroidLib'      // 项目的主页
def gitUrl = 'https://github.com/as11051105/MyAndroidLib.git'   // Git仓库的url

group = "com.zxy"// 唯一包名，比如compile 'com.zxy:androidlib:1.0.0'中的com.zxy就是这里配置的
//libName = "androidlib"//发布到JCenter上的项目名字，必须填写,，比如compile 'com.zxy:androidlib:1.0.0'中的androidlib就是这里配置的
version = "1.0.1"// 版本号，下次更新是只需要更改版本号即可
/**  上面配置后上传至jcenter后的编译路径是这样的： compile 'cn.zxy:androidlib:1.0.0'  **/


allprojects {
    repositories {
        maven { url 'https://zxyi.bintray.com/androidlib' }
    }
}

// 生成jar包的task
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

// 生成jarDoc的task，不需要修改。
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    // destinationDir = file("../javadoc/")
    failOnError false // 忽略注释语法错误，如果用jdk1.8你的注释写的不规范就编译不过。
}

// 生成javaDoc的jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

//jav doc采用utf-8编码否则会报“GBK的不可映射字符”错误
javadoc {
    options{
        encoding "UTF-8"
        charSet 'UTF-8'
    }
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // 生成pom.xml和参数
        pom {
            project {
                packaging 'aar'
                // 项目描述，复制我的话，这里需要修改。
                name 'lib For Android'// 可选，项目名称。
                description 'This is a androidlib.'// 可选，项目描述。
                url siteUrl
                // 软件开源协议，现在一般都是Apache License2.0
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                //填写开发者基本信息
                developers {
                    developer {
                        id 'as1105'     // 开发者的id
                        name 'as1105'   // 开发者名字
                        email 'as11051105@hotmail.com'// 开发者邮箱
                    }
                }
                scm {
                    connection gitUrl // Git仓库地址
                    developerConnection gitUrl // Git仓库地址
                    url siteUrl // 项目主页
                }
            }
        }
    }
}

// 这里是读取Bintray相关的信息，我们上传项目到github上的时候会把gradle文件传上去，所以不要把帐号密码的信息直接写在这里，写在local.properties中，这里动态读取。
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")// Bintray的用户名
    key = properties.getProperty("bintray.apikey")// Bintray保存的ApiKey

    configurations = ['archives']
    pkg {
        repo = 'maven' // 上传到maven库。（这里要特别注意，如果写了maven报404错误，请在bintray创建一个仓库，这里填改成你创建的仓库的名字，如何创建请看下图。）
        name = 'androidlib'   //发布到JCenter上的项目名字
        userOrg = 'as1105'      //bintray注册的用户名
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}





